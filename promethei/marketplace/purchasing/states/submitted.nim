import pkg/metrics

import ../../../logutils
import ../../../utils/exceptions
import ../statemachine
import ./types

logScope:
  topics = "marketplace purchases submitted"

declareCounter(promethei_purchases_submitted, "promethei purchases submitted")

method `$`*(state: PurchaseSubmitted): string =
  "submitted"

method run*(
    state: PurchaseSubmitted, machine: Machine
): Future[?State] {.async: (raises: []).} =
  promethei_purchases_submitted.inc()
  let purchase = Purchase(machine)
  let request = !purchase.request
  let marketplace = purchase.marketplace
  let clock = purchase.clock

  info "Request submitted, waiting for slots to be filled",
    requestId = purchase.requestId

  proc wait() {.async.} =
    let done = newAsyncEvent()
    proc callback(_: RequestId) =
      done.fire()

    let subscription = await marketplace.subscribeFulfillment(request.id, callback)
    await done.wait()
    await subscription.unsubscribe()

  proc withTimeout(future: Future[void]) {.async.} =
    let expiry = (await marketplace.requestExpiresAt(request.id)) + 1'u8
    trace "waiting for request fulfillment or expiry", expiry
    await future.withTimeout(clock, expiry.toSecondsSince1970)

  try:
    await wait().withTimeout()
  except Timeout:
    return some State(PurchaseCancelled())
  except CancelledError as e:
    trace "PurchaseSubmitted.run was cancelled", error = e.msgDetail
  except CatchableError as e:
    error "Error during PurchaseSubmitted.run", error = e.msgDetail
    return some State(PurchaseErrored(error: e))

  return some State(PurchaseStarted())
