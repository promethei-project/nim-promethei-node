import std/sequtils
import std/tables
import pkg/chronos
import pkg/questionable
import pkg/questionable/results
import ../../errors
import ../../logutils
import ../../rng
import ../../utils
import ../../utils/asyncheapqueue
import ../contracts/requests

logScope:
  topics = "marketplace slotqueue"

type
  OnProcessSlot* = proc(item: SlotQueueItem): Future[void] {.
    gcsafe, async: (raises: [CancelledError])
  .}

  # Non-ref obj copies value when assigned, preventing accidental modification
  # of values which could cause an incorrect order (eg
  # ``slotQueue[1].collateral = 1`` would cause ``collateral`` to be updated,
  # but the heap invariant would no longer be honoured. When non-ref, the
  # compiler can ensure that statement will fail).
  SlotQueueItem* = object
    requestId: RequestId
    slotIndex: uint16
    ask: StorageAsk
    expiry: ?StorageTimestamp
    repairReward: Tokens
    availabilitiesVersion: uint64 # used to check whether availabilities have changed

  # don't need to -1 to prevent overflow when adding 1 (to always allow push)
  # because AsyncHeapQueue size is of type `int`, which is larger than `uint16`
  SlotQueueSize = range[1'u16 .. uint16.high]

  SlotQueue* = ref object
    maxWorkers: int
    onProcessSlot: ?OnProcessSlot
    queue: AsyncHeapQueue[SlotQueueItem]
    running: bool
    workers: seq[Future[void].Raising([])]
    unpaused: AsyncEvent
    availabilitiesVersion: uint64 # increases every time the availabilities change

  SlotQueueError = object of PrometheiError
  SlotQueueItemExistsError* = object of SlotQueueError
  SlotQueueItemNotExistsError* = object of SlotQueueError
  SlotsOutOfRangeError* = object of SlotQueueError
  QueueNotRunningError* = object of SlotQueueError

# Number of concurrent workers used for processing SlotQueueItems
const DefaultMaxWorkers = 3

# Cap slot queue size to prevent unbounded growth and make sifting more
# efficient. Max size is not equivalent to the number of slots a host can
# service, which is limited by host availabilities and new requests circulating
# the network. Additionally, each new request/slot in the network will be
# included in the queue if it is higher priority than any of the exisiting
# items. Older slots should be unfillable over time as other hosts fill the
# slots.
const DefaultMaxSize = 128'u16

proc profitability(item: SlotQueueItem): Tokens =
  item.ask.pricePerSlot + item.repairReward

proc `<`*(a, b: SlotQueueItem): bool =
  # for A to have a higher priority than B (in a min queue), A must be less than
  # B.
  var scoreA: uint8 = 0
  var scoreB: uint8 = 0

  proc addIf(score: var uint8, condition: bool, addition: int) =
    if condition:
      score += 1'u8 shl addition

  scoreA.addIf(a.availabilitiesVersion < b.availabilitiesVersion, 4)
  scoreB.addIf(a.availabilitiesVersion > b.availabilitiesVersion, 4)

  scoreA.addIf(a.profitability > b.profitability, 3)
  scoreB.addIf(a.profitability < b.profitability, 3)

  scoreA.addIf(a.ask.collateralPerSlot < b.ask.collateralPerSlot, 2)
  scoreB.addIf(a.ask.collateralPerSlot > b.ask.collateralPerSlot, 2)

  if expiryA =? a.expiry and expiryB =? b.expiry:
    scoreA.addIf(expiryA > expiryB, 1)
    scoreB.addIf(expiryA < expiryB, 1)

  return scoreA > scoreB

proc `==`*(a, b: SlotQueueItem): bool =
  a.requestId == b.requestId and a.slotIndex == b.slotIndex

proc new*(
    _: type SlotQueue,
    maxWorkers = DefaultMaxWorkers,
    maxSize: SlotQueueSize = DefaultMaxSize,
): SlotQueue =
  doAssert maxWorkers > 0, "maxWorkers must be positive"
  doAssert maxWorkers.uint16 <= maxSize, "maxWorkers must be less than maxSize"

  SlotQueue(
    maxWorkers: maxWorkers,
    # Add 1 to always allow for an extra item to be pushed onto the queue
    # temporarily. After push (and sort), the bottom-most item will be deleted
    queue: newAsyncHeapQueue[SlotQueueItem](maxSize.int + 1),
    running: false,
    unpaused: newAsyncEvent(),
    availabilitiesVersion: 1,
  )
  # avoid instantiating `workers` in constructor to avoid side effects in
  # `newAsyncQueue` procedure

proc init(
    _: type SlotQueueItem,
    requestId: RequestId,
    slotIndex: uint16,
    ask: StorageAsk,
    expiry: ?StorageTimestamp,
    repairReward = Tokens.init(0),
    availabilitiesVersion = 0'u64,
): SlotQueueItem =
  SlotQueueItem(
    requestId: requestId,
    slotIndex: slotIndex,
    ask: ask,
    expiry: expiry,
    repairReward: repairReward,
    availabilitiesVersion: availabilitiesVersion,
  )

proc init*(
    _: type SlotQueueItem,
    requestId: RequestId,
    slotIndex: uint16,
    ask: StorageAsk,
    expiry: StorageTimestamp,
    repairReward = Tokens.init(0),
    availabilitiesVersion = 0'u64,
): SlotQueueItem =
  SlotQueueItem.init(
    requestId, slotIndex, ask, some expiry, repairReward, availabilitiesVersion
  )

proc init*(
    _: type SlotQueueItem,
    request: StorageRequest,
    slotIndex: uint16,
    repairReward = Tokens.init(0),
    availabilitiesVersion = 0'u64,
): SlotQueueItem =
  SlotQueueItem.init(
    request.id, slotIndex, request.ask, StorageTimestamp.none, repairReward,
    availabilitiesVersion,
  )

proc init(
    _: type SlotQueueItem,
    requestId: RequestId,
    ask: StorageAsk,
    expiry: ?StorageTimestamp,
    repairReward = Tokens.init(0),
): seq[SlotQueueItem] {.raises: [SlotsOutOfRangeError].} =
  if not ask.slots.inRange:
    raise newException(SlotsOutOfRangeError, "Too many slots")

  var i = 0'u16
  proc initSlotQueueItem(): SlotQueueItem =
    let item = SlotQueueItem.init(requestId, i, ask, expiry, repairReward)
    inc i
    return item

  var items = newSeqWith(ask.slots.int, initSlotQueueItem())
  Rng.instance.shuffle(items)
  return items

proc init*(
    _: type SlotQueueItem,
    requestId: RequestId,
    ask: StorageAsk,
    expiry: StorageTimestamp,
    repairReward = Tokens.init(0),
): seq[SlotQueueItem] {.raises: [SlotsOutOfRangeError].} =
  SlotQueueItem.init(requestId, ask, some expiry, repairReward)

proc init*(
    _: type SlotQueueItem, request: StorageRequest, repairReward = Tokens.init(0)
): seq[SlotQueueItem] {.raises: [SlotsOutOfRangeError].} =
  return
    SlotQueueItem.init(request.id, request.ask, StorageTimestamp.none, repairReward)

proc inRange*(val: SomeUnsignedInt): bool =
  val.uint16 in SlotQueueSize.low .. SlotQueueSize.high

proc requestId*(self: SlotQueueItem): RequestId =
  self.requestId

proc slotIndex*(self: SlotQueueItem): uint16 =
  self.slotIndex

proc ask*(self: SlotQueueItem): StorageAsk =
  self.ask

proc slotSize*(self: SlotQueueItem): uint64 =
  self.ask.slotSize

proc duration*(self: SlotQueueItem): StorageDuration =
  self.ask.duration

proc pricePerBytePerSecond*(self: SlotQueueItem): TokensPerSecond =
  self.ask.pricePerBytePerSecond

proc collateralPerByte*(self: SlotQueueItem): Tokens =
  self.ask.collateralPerByte

proc running*(self: SlotQueue): bool =
  self.running

proc len*(self: SlotQueue): int =
  self.queue.len

proc size*(self: SlotQueue): int =
  self.queue.size - 1

proc paused*(self: SlotQueue): bool =
  not self.unpaused.isSet

proc `$`*(self: SlotQueue): string =
  $self.queue

proc `onProcessSlot=`*(self: SlotQueue, onProcessSlot: OnProcessSlot) =
  self.onProcessSlot = some onProcessSlot

proc contains*(self: SlotQueue, item: SlotQueueItem): bool =
  self.queue.contains(item)

proc pause*(self: SlotQueue) =
  # set unpaused flag to false - coroutines will block on unpaused.wait()
  self.unpaused.clear()

proc unpause*(self: SlotQueue) =
  # set unpaused flag to true - unblocks coroutines waiting on unpaused.wait()
  self.unpaused.fire()

proc push*(self: SlotQueue, item: SlotQueueItem): ?!void {.raises: [].} =
  logScope:
    requestId = item.requestId
    slotIndex = item.slotIndex
    availabilitiesVersion = item.availabilitiesVersion

  trace "pushing item to queue"

  if not self.running:
    let err = newException(QueueNotRunningError, "queue not running")
    return failure(err)

  if self.contains(item):
    let err = newException(SlotQueueItemExistsError, "item already exists")
    return failure(err)

  if err =? self.queue.pushNoWait(item).mapFailure.errorOption:
    return failure(err)

  if self.queue.full():
    # delete the last item
    self.queue.del(self.queue.size - 1)

  doAssert self.queue.len <= self.queue.size - 1

  # when slots are pushed to the queue, the queue should be unpaused if it was
  # paused
  if self.paused and item.availabilitiesVersion < self.availabilitiesVersion:
    trace "unpausing queue after new slot pushed"
    self.unpause()

  return success()

proc push*(self: SlotQueue, items: seq[SlotQueueItem]): ?!void =
  for item in items:
    if err =? self.push(item).errorOption:
      return failure(err)

  return success()

proc findByRequest(self: SlotQueue, requestId: RequestId): seq[SlotQueueItem] =
  var items: seq[SlotQueueItem] = @[]
  for item in self.queue.items:
    if item.requestId == requestId:
      items.add item
  return items

proc delete*(self: SlotQueue, item: SlotQueueItem) =
  logScope:
    requestId = item.requestId
    slotIndex = item.slotIndex

  trace "removing item from queue"

  if not self.running:
    trace "cannot delete item from queue, queue not running"
    return

  self.queue.delete(item)

proc delete*(self: SlotQueue, requestId: RequestId, slotIndex: uint16) =
  let item = SlotQueueItem(requestId: requestId, slotIndex: slotIndex)
  self.delete(item)

proc delete*(self: SlotQueue, requestId: RequestId) =
  let items = self.findByRequest(requestId)
  for item in items:
    self.delete(item)

proc `[]`*(self: SlotQueue, i: Natural): SlotQueueItem =
  self.queue[i]

proc availabilityChanged*(self: SlotQueue) =
  inc self.availabilitiesVersion
  if self.paused:
    trace "unpausing queue after availabilities changed"
    self.unpause()

proc runWorker(self: SlotQueue) {.async: (raises: []).} =
  trace "slot queue worker loop started"
  while self.running:
    try:
      if self.paused:
        trace "Queue is paused, waiting for new slots or availabilities to be modified/added"

      # block until unpaused is true/fired, ie wait for queue to be unpaused
      await self.unpaused.wait()

      var item = await self.queue.pop() # if queue empty, wait here for new items

      logScope:
        reqId = item.requestId
        slotIdx = item.slotIndex
        availabilitiesVersion = item.availabilitiesVersion

      if not self.running: # may have changed after waiting for pop
        trace "not running, exiting"
        break

      if item.availabilitiesVersion == self.availabilitiesVersion:
        trace "processing already seen item, pausing queue",
          reqId = item.requestId, slotIdx = item.slotIndex
        self.pause()
        # put item back in queue so that if other items are pushed while paused,
        # it will be sorted accordingly. Otherwise, this item would be processed
        # immediately (with priority over other items) once unpaused
        trace "readding seen item back into the queue"
        discard self.push(item) # on error, drop the item and continue
        continue

      trace "processing item"
      without onProcessSlot =? self.onProcessSlot:
        raiseAssert "slot queue onProcessSlot not set"

      item.availabilitiesVersion = self.availabilitiesVersion
      await onProcessSlot(item)
    except CancelledError:
      trace "slot queue worker cancelled"
      break
    except CatchableError as e: # raised from self.queue.pop()
      warn "slot queue worker error encountered during processing", error = e.msg
  trace "slot queue worker loop stopped"

proc start*(self: SlotQueue) =
  if self.running:
    return

  trace "starting slot queue"

  self.running = true

  # Add initial workers to the `AsyncHeapQueue`. Once a worker has completed its
  # task, a new worker will be pushed to the queue
  for i in 0 ..< self.maxWorkers:
    let worker = self.runWorker()
    self.workers.add(worker)

proc stop*(self: SlotQueue) {.async: (raises: []).} =
  if not self.running:
    return

  trace "stopping slot queue"

  self.running = false

  for worker in self.workers:
    await noCancel worker.cancelAndWait()
