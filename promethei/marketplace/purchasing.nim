import std/tables
import pkg/stint
import pkg/chronos
import pkg/questionable
import pkg/questionable/results
import pkg/nimcrypto
import ../clock
import ./abstractmarketplace
import ./purchasing/purchase

export purchase.Purchase
export purchase.PurchaseId
export purchase.id
export purchase.state
export purchase.error

type
  Purchasing* = ref object
    marketplace*: AbstractMarketplace
    clock: Clock
    purchases: Table[PurchaseId, Purchase]
    proofProbability*: UInt256

  PurchaseTimeout* = Timeout

const DefaultProofProbability = 100.u256

func durationLimit*(purchasing: Purchasing): StorageDuration =
  purchasing.marketplace.requestDurationLimit

proc new*(
    _: type Purchasing, marketplace: AbstractMarketplace, clock: Clock
): Purchasing =
  Purchasing(
    marketplace: marketplace, clock: clock, proofProbability: DefaultProofProbability
  )

proc load*(purchasing: Purchasing) {.async.} =
  # Re-adopt requests from the onchain StorageRequested history after a restart.
  let requestDurationLimit = purchasing.marketplace.requestDurationLimit()
  let blocksAgo = int(requestDurationLimit.u64 div 12)
  let requestEvents =
    await purchasing.marketplace.queryPastStorageRequestedEvents(blocksAgo = blocksAgo)
  let signer = await purchasing.marketplace.getSigner()
  for event in requestEvents:
    without request =? (await purchasing.marketplace.getRequest(event.requestId)):
      continue
    if request.client != signer:
      continue
    let purchase =
      Purchase.new(event.requestId, purchasing.marketplace, purchasing.clock)
    purchase.load()
    purchasing.purchases[purchase.id] = purchase

proc start*(
    purchasing: Purchasing
): Future[?!void] {.async: (raises: [CancelledError]).} =
  try:
    await purchasing.load()
    success()
  except CancelledError as error:
    raise error
  except CatchableError as error:
    failure error

proc stop*(purchasing: Purchasing) {.async: (raises: []).} =
  discard

proc populate*(
    purchasing: Purchasing, request: StorageRequest
): Future[?!StorageRequest] {.async: (raises: [CancelledError]).} =
  var populated = request
  if populated.ask.proofProbability == 0.u256:
    populated.ask.proofProbability = purchasing.proofProbability
  if populated.nonce == Nonce.default:
    var id = populated.nonce.toArray
    doAssert randomBytes(id) == 32
    populated.nonce = Nonce(id)
  try:
    populated.client = await purchasing.marketplace.getSigner()
  except MarketplaceError as error:
    return failure error
  success populated

proc purchase*(
    purchasing: Purchasing, request: StorageRequest
): Future[?!Purchase] {.async: (raises: [CancelledError]).} =
  let request = ?await purchasing.populate(request)
  let purchase = Purchase.new(request, purchasing.marketplace, purchasing.clock)
  purchase.start()
  purchasing.purchases[purchase.id] = purchase
  success purchase

func getPurchase*(purchasing: Purchasing, id: PurchaseId): ?Purchase =
  if purchasing.purchases.hasKey(id):
    some purchasing.purchases[id]
  else:
    none Purchase

func getPurchases*(purchasing: Purchasing): seq[PurchaseId] =
  var pIds: seq[PurchaseId] = @[]
  for key in purchasing.purchases.keys:
    pIds.add(key)
  return pIds
