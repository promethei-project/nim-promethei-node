## Copyright (c) 2025 Promethei Authors
## Copyright (c) 2021 Status Research & Development GmbH
## Licensed under either of
##  * Apache License, version 2.0, ([LICENSE-APACHE](LICENSE-APACHE))
##  * MIT license ([LICENSE-MIT](LICENSE-MIT))
## at your option.
## This file may not be copied, modified, or distributed except according to
## those terms.

{.push raises: [].}

import std/options
import std/sequtils

import pkg/results
import pkg/chronos
import pkg/questionable/results

export results

type
  PrometheiError* = object of CatchableError # base error

  FinishedFailed*[T] = tuple[success: seq[Future[T]], failure: seq[Future[T]]]

template mapFailure*[T, V, E](
    exp: Result[T, V], exc: typedesc[E]
): Result[T, ref CatchableError] =
  ## Convert `Result[T, V]` to `Result[T, ref CatchableError]`, wrapping
  ## the error in `exc`.

  exp.mapErr(
    proc(e: V): ref CatchableError =
      (ref exc)(msg: $e)
  )

template mapFailure*[T, V](exp: Result[T, V]): Result[T, ref CatchableError] =
  mapFailure(exp, PrometheiError)

# TODO: using a template here, causes bad codegen
func toFailure*[T](exp: Option[T]): Result[T, ref CatchableError] {.inline.} =
  if exp.isSome:
    success exp.get
  else:
    T.failure("Option is None")

proc allFinishedFailed*[T](
    futs: auto
): Future[FinishedFailed[T]] {.async: (raises: [CancelledError]).} =
  ## Check if all futures have finished or failed
  ##
  ## TODO: wip, not sure if we want this - at the minimum,
  ## we should probably avoid the async transform
  ##

  var res: FinishedFailed[T] = (@[], @[])
  await allFutures(futs)
  for f in futs:
    if f.failed or f.cancelled:
      res.failure.add f
    else:
      res.success.add f

  return res

proc allFinishedValues*[T](
    futs: auto
): Future[?!seq[T]] {.async: (raises: [CancelledError]).} =
  ## If all futures have finished, return corresponding values,
  ## otherwise return failure
  ##

  # wait for all futures to be either completed, failed or canceled
  await allFutures(futs)

  let numOfFailed = futs.countIt(it.failed)

  if numOfFailed > 0:
    return failure "Some futures failed (" & $numOfFailed & "))"

  # here, we know there are no failed futures in "futs"
  # and we are only interested in those that completed successfully
  return success futs.filterIt(it.finished).mapIt(it.value)

template catchAsync*(body: typed): Result[type(body), ref CatchableError] =
  ## Catch exceptions for body and store them in the Result
  ##
  ## NOTE: Adopted from Results to propagate async cancellations
  ##
  ## ```
  ## let r = catch: someFuncThatMayRaise()
  ## ```
  type R = Result[type(body), ref CatchableError]

  try:
    when type(body) is void:
      body
      R.ok()
    else:
      R.ok(body)
  except CancelledError as exc:
    raise exc
  except CatchableError as exc:
    R.err(exc)
