import pkg/chronos
import pkg/questionable/results
import pkg/promethei/chunker

export chunker

type MockChunker* = Chunker

proc new*(
    T: type MockChunker,
    dataset: openArray[byte],
    chunkSize: int | NBytes,
    pad: bool = false,
): MockChunker =
  ## Create a chunker that produces data
  ##

  let
    chunkSize = chunkSize.NBytes
    dataset = @dataset

  var consumed = 0
  proc reader(
      data: ChunkBuffer, len: int
  ): Future[?!int] {.gcsafe, async: (raises: [CancelledError]).} =
    if consumed >= dataset.len:
      return success 0

    var read = 0
    while read < len and read < chunkSize.int and (consumed + read) < dataset.len:
      data[read] = dataset[consumed + read]
      read.inc

    consumed += read
    return success read

  Chunker.new(reader = reader, pad = pad, chunkSize = chunkSize)
