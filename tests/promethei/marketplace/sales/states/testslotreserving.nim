import pkg/chronos
import pkg/questionable
import pkg/promethei/marketplace/contracts/requests
import pkg/promethei/marketplace/sales/states/slotreserving
import pkg/promethei/marketplace/sales/states/downloading
import pkg/promethei/marketplace/sales/states/cancelled
import pkg/promethei/marketplace/sales/states/failed
import pkg/promethei/marketplace/sales/states/ignored
import pkg/promethei/marketplace/sales/states/types
import pkg/promethei/marketplace/sales/salesagent
import pkg/promethei/marketplace/sales/salescontext
import pkg/promethei/stores/repostore
import ../../../../asynctest
import ../../../helpers
import ../../../examples
import ../../../helpers/mockmarketplace
import ../../../helpers/mockclock

asyncchecksuite "sales state 'SlotReserving'":
  let request = StorageRequest.example
  let slotIndex = request.ask.slots div 2
  let slot = Slot(request: request, slotIndex: slotIndex)
  var marketplace: MockMarketplace
  var clock: MockClock
  var agent: SalesAgent
  var state: SaleSlotReserving
  var context: SalesContext

  setup:
    marketplace = MockMarketplace.new()
    clock = MockClock.new()

    state = SaleSlotReserving.new()
    context = SalesContext(marketplace: marketplace, clock: clock)
    var slotInfo = SlotInfo.init(slot.id)
    slotInfo.slot = slot
    agent = newSalesAgent(context, slotInfo)

  test "switches to cancelled state when request expires":
    let next = state.onCancelled(request)
    check !next of SaleCancelled

  test "switches to failed state when request fails":
    let next = state.onFailed(request)
    check !next of SaleFailed

  test "run switches to downloading when slot successfully reserved":
    let next = await state.run(agent)
    check !next of SaleDownloading

  test "run switches to ignored when slot reservation not allowed":
    marketplace.setCanReserveSlot(false)
    let next = await state.run(agent)
    check !next of SaleIgnored

  test "run switches to errored when slot reservation errors":
    let error = newException(MarketplaceError, "some error")
    marketplace.setErrorOnReserveSlot(error)
    let next = !(await state.run(agent))
    check next of SaleErrored
    let errored = SaleErrored(next)
    check errored.error == error

  test "run switches to ignored when reservation is not allowed":
    let error =
      newException(SlotReservationNotAllowedError, "Reservation is not allowed")
    marketplace.setErrorOnReserveSlot(error)
    let next = !(await state.run(agent))
    check next of SaleIgnored
    check SaleIgnored(next).reprocessSlot == false
