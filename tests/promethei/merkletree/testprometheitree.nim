import std/sequtils

import pkg/unittest2
import pkg/questionable/results
import pkg/stew/byteutils
import pkg/libp2p

import pkg/promethei/prometheitypes
import pkg/promethei/merkletree
import pkg/promethei/utils/digest

import ./helpers
import ./generictreetests

# TODO: Generalize to other hashes

const
  data = [
    "00000000000000000000000000000001".toBytes,
    "00000000000000000000000000000002".toBytes,
    "00000000000000000000000000000003".toBytes,
    "00000000000000000000000000000004".toBytes,
    "00000000000000000000000000000005".toBytes,
    "00000000000000000000000000000006".toBytes,
    "00000000000000000000000000000007".toBytes,
    "00000000000000000000000000000008".toBytes,
    "00000000000000000000000000000009".toBytes,
    "00000000000000000000000000000010".toBytes,
  ]
  sha256 = Sha256HashCodec

suite "Test PrometheiTree":
  test "Cannot init tree without any multihash leaves":
    check:
      PrometheiTree.init(leaves = newSeq[MultiHash]()).isErr

  test "Cannot init tree without any cid leaves":
    check:
      PrometheiTree.init(leaves = newSeq[Cid]()).isErr

  test "Cannot init tree without any byte leaves":
    check:
      PrometheiTree.init(sha256, leaves = newSeq[ByteHash]()).isErr

  test "Should build tree from multihash leaves":
    var expectedLeaves = data.mapIt(MultiHash.digest($sha256, it).tryGet())

    var tree = PrometheiTree.init(leaves = expectedLeaves)
    check:
      tree.isOk
      tree.get().leaves == expectedLeaves.mapIt(it.digestBytes)
      tree.get().mcodec == sha256

  test "Should build tree from cid leaves":
    var expectedLeaves = data.mapIt(
      Cid.init(CidVersion.CIDv1, BlockCodec, MultiHash.digest($sha256, it).tryGet).tryGet
    )

    let tree = PrometheiTree.init(leaves = expectedLeaves)

    check:
      tree.isOk
      tree.get().leaves == expectedLeaves.mapIt(it.mhash.tryGet.digestBytes)
      tree.get().mcodec == sha256

  test "Should build from raw digestbytes (should not hash leaves)":
    let tree = PrometheiTree.init(sha256, leaves = data).tryGet

    check:
      tree.mcodec == sha256
      tree.leaves == data

  test "Should build from nodes":
    let
      tree = PrometheiTree.init(sha256, leaves = data).tryGet
      fromNodes = PrometheiTree.fromNodes(
        nodes = toSeq(tree.nodes), nleaves = tree.leavesCount
      ).tryGet

    check:
      tree.mcodec == sha256
      tree == fromNodes

let
  zero: seq[byte] = newSeq[byte](sha256.digestSize.tryGet)
  compress = proc(x, y: seq[byte], key: ByteTreeKey): seq[byte] =
    compress(x, y, key).tryGet

  makeTree = proc(data: seq[seq[byte]]): PrometheiTree =
    PrometheiTree.init(sha256, leaves = data).tryGet

testGenericTree("PrometheiTree", @data, zero, compress, makeTree)
