import std/options

import ../../../asynctest

import pkg/chronos
import pkg/poseidon2
import pkg/serde/json
import pkg/kvstore
import pkg/taskpools

import pkg/promethei/slots {.all.}
import pkg/promethei/slots/types {.all.}
import pkg/promethei/merkletree
import pkg/promethei/prometheitypes
import pkg/promethei/manifest
import pkg/promethei/stores

import ./helpers
import ../helpers
import ../../helpers

suite "Test Circom Compat Backend - control inputs":
  let
    r1cs = "tests/circuits/fixtures/proof_main.r1cs"
    wasm = "tests/circuits/fixtures/proof_main.wasm"
    zkey = "tests/circuits/fixtures/proof_main.zkey"

  var
    circom: CircomCompatBackendRef
    proofInputs: ProofInputs[Poseidon2Hash]

  setup:
    let
      inputData = readFile("tests/circuits/fixtures/input.json")
      inputJson = !JsonNode.parse(inputData)

    proofInputs = Poseidon2Hash.jsonToProofInput(inputJson)
    circom = CircomCompatBackendRef.new(r1cs, wasm, zkey).tryGet

  teardown:
    circom.release() # this comes from the rust FFI

  test "Should verify with correct inputs":
    let proof = (await circom.prove(proofInputs)).tryGet
    check (await circom.verify(proof, proofInputs)).tryGet

  test "Should not verify with incorrect inputs":
    proofInputs.slotIndex = 1 # change slot index

    let proof = (await circom.prove(proofInputs)).tryGet
    check (await circom.verify(proof, proofInputs)).tryGet == false

suite "Test Circom Compat Backend":
  let
    ecK = 2
    ecM = 2
    slotId = 3
    samples = 5
    numDatasetBlocks = 8
    blockSize = DefaultBlockSize
    cellSize = DefaultCellSize

    r1cs = "tests/circuits/fixtures/proof_main.r1cs"
    wasm = "tests/circuits/fixtures/proof_main.wasm"
    zkey = "tests/circuits/fixtures/proof_main.zkey"

  var
    store: RepoStore
    manifest: Manifest
    protected: Manifest
    verifiable: Manifest
    circom: CircomCompatBackendRef
    proofInputs: ProofInputs[Poseidon2Hash]
    challenge: array[32, byte]
    builder: Poseidon2Builder
    sampler: Poseidon2Sampler
    tp: Taskpool

  setup:
    tp = Taskpool.new(num_threads = 4)
    let
      repoDs = SQLiteKVStore.new(SqliteMemory, tp).tryGet()
      metaDs = SQLiteKVStore.new(SqliteMemory, tp).tryGet()

    store = RepoStore.new(repoDs, metaDs)

    (manifest, protected, verifiable) = await createVerifiableManifest(
      store, numDatasetBlocks, ecK, ecM, blockSize, cellSize, tp
    )

    builder = Poseidon2Builder.new(store, store, verifiable).tryGet
    sampler = Poseidon2Sampler.new(slotId, store, builder).tryGet

    circom = CircomCompatBackendRef.new(r1cs, wasm, zkey).tryGet
    challenge = 1234567.toF.toBytes.toArray32

    proofInputs = (await sampler.getProofInput(challenge, samples)).tryGet

  teardown:
    circom.release() # this comes from the rust FFI
    await store.close()
    tp.shutdown()

  test "Should verify with correct input":
    var proof = (await circom.prove(proofInputs)).tryGet
    check (await circom.verify(proof, proofInputs)).tryGet

  test "Should not verify with incorrect input":
    proofInputs.slotIndex = 1 # change slot index

    let proof = (await circom.prove(proofInputs)).tryGet
    check (await circom.verify(proof, proofInputs)).tryGet == false
