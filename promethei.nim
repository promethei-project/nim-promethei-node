## Copyright (c) 2025 Promethei Authors
## Copyright (c) 2021 Status Research & Development GmbH
## Licensed under either of
##  * Apache License, version 2.0, ([LICENSE-APACHE](LICENSE-APACHE))
##  * MIT license ([LICENSE-MIT](LICENSE-MIT))
## at your option.
## This file may not be copied, modified, or distributed except according to
## those terms.

import pkg/chronos
import pkg/questionable
import pkg/confutils
import pkg/confutils/std/net
import pkg/confutils/toml/defs as confTomlDefs
import pkg/confutils/toml/std/net as confTomlNet
import pkg/confutils/toml/std/uri as confTomlUri
import pkg/toml_serialization
import pkg/libp2p

import ./promethei/conf
import ./promethei/promethei
import ./promethei/logutils
import ./promethei/units
import ./promethei/utils/keyutils
import ./promethei/prometheitypes

export promethei, conf, libp2p, chronos, logutils

when isMainModule:
  import std/os
  import pkg/confutils/defs
  import ./promethei/utils/fileutils

  logScope:
    topics = "promethei"

  const defaultConfigFile = "config.toml"

  when defined(posix):
    import system/ansi_c

  when defined(linux):
    # Pin the mmap threshold below the block size: block buffers stay on
    # the mmap path and return memory to the OS on free. Setting it
    # explicitly also disables the dynamic ratchet that demotes them.
    const M_MMAP_THRESHOLD = -3
    proc mallopt(param: cint, value: cint): cint {.importc, header: "<malloc.h>".}
    discard mallopt(M_MMAP_THRESHOLD, 64 * 1024)

  type NodeStatus {.pure.} = enum
    Stopped
    Stopping
    Running

  proc addConfigFileSources(
      config: NodeConf, sources: auto
  ) {.gcsafe, raises: [ConfigurationError].} =
    if configFile =? config.configFile:
      sources.addConfigFile(Toml, configFile)
    # If a local config.toml file exists, use it automatically.
    elif fileExists(defaultConfigFile):
      sources.addConfigFile(Toml, InputFile(defaultConfigFile))

  let config = NodeConf.load(
    version = nodeFullVersion,
    envVarsPrefix = "promethei",
    secondarySources = addConfigFileSources,
  )
  config.setupLogging()
  config.setupMetrics()

  if not (checkAndCreateDataDir((config.dataDir).string)):
    # We are unable to access/create data folder or data folder's
    # permissions are insecure.
    quit QuitFailure

  if config.prover and not (checkAndCreateDataDir((config.circuitDir).string)):
    quit QuitFailure

  trace "Data dir initialized", dir = $config.dataDir

  if not (checkAndCreateDataDir((config.dataDir / "repo"))):
    # We are unable to access/create data folder or data folder's
    # permissions are insecure.
    quit QuitFailure

  trace "Repo dir initialized", dir = config.dataDir / "repo"

  var
    state: NodeStatus
    shutdown: Future[void]

  let
    keyPath =
      if isAbsolute(config.netPrivKeyFile):
        config.netPrivKeyFile
      else:
        config.dataDir / config.netPrivKeyFile

    privateKey = setupKey(keyPath).expect("Should setup private key!")
    server =
      try:
        NodeServer.new(config, privateKey)
      except Exception as exc:
        error "Failed to start Promethei Node", msg = exc.msg
        quit QuitFailure

  ## Ctrl+C handling
  proc doShutdown() =
    shutdown = server.stop()
    state = NodeStatus.Stopping

    notice "Stopping Promethei Node"

  proc controlCHandler() {.noconv.} =
    when defined(windows):
      # workaround for https://github.com/nim-lang/Nim/issues/4057
      try:
        setupForeignThreadGc()
      except Exception as exc:
        raiseAssert exc.msg
        # shouldn't happen
    notice "Shutting down after having received SIGINT"

    doShutdown()

  try:
    setControlCHook(controlCHandler)
  except Exception as exc: # TODO Exception
    warn "Cannot set ctrl-c handler", msg = exc.msg

  # equivalent SIGTERM handler
  when defined(posix):
    proc SIGTERMHandler(signal: cint) {.noconv.} =
      notice "Shutting down after having received SIGTERM"

      doShutdown()

    c_signal(ansi_c.SIGTERM, SIGTERMHandler)

  try:
    waitFor server.start()
  except CatchableError as error:
    error "Promethei Node failed to start", error = error.msg
    # XXX ideally we'd like to issue a stop instead of quitting cold turkey,
    #   but this would mean we'd have to fix the implementation of all
    #   services so they won't crash if we attempt to stop them before they
    #   had a chance to start (currently you'll get a SISGSEV if you try to).
    quit QuitFailure

  state = NodeStatus.Running
  while state == NodeStatus.Running:
    try:
      # poll chronos
      chronos.poll()
    except Exception as exc:
      error "Unhandled exception in async proc, aborting", msg = exc.msg
      # raise exc # uncomment for stack trace
      quit QuitFailure

  try:
    # signal handlers guarantee that the shutdown Future will
    # be assigned before state switches to Stopping
    waitFor shutdown
  except CatchableError as error:
    error "Promethei Node didn't shutdown correctly", error = error.msg
    # raise exc # uncomment for stacktrace
    quit QuitFailure

  notice "Exited Promethei Node"
